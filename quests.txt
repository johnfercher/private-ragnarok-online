//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,164,171,2	script	Quests#1	4_F_HUNTER_EVIL,{ 
	OnTouch:
		callfunc "qshop"; 
	end;
	
	OnInit:
		questinfo 0, QTYPE_QUEST,3;
	end;
}

// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot; function convertype;
	OnInit:
		freeloop(1);

		// -----------------------------------------------------------
		//  Basic shop settings.
		// -----------------------------------------------------------

		set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
		set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
		set .ShowID,1;  	// Show item IDs? (1: yes / 0: no)
		set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
		set .MaxStack,100;	// Max number of quest items purchased at one time.

		// -----------------------------------------------------------
		//  Points variable -- optional quest requirement.
		//	setarray .Points$[0],"<variable name>","<display name>";
		// -----------------------------------------------------------

		setarray .Points$[0],"#CASHPOINTS","Cash Points";

		// -----------------------------------------------------------
		//  Shop IDs -- to add shops, copy dummy data at bottom of file.
		//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
		// -----------------------------------------------------------

		setarray .Shops$[1], "Cima", "Baixo", "Meio", "Meio / Baixo", "Cima / Meio", "Cima / Meio / Baixo", "Acessórios", "Capas", "Botas", "Escudos", "Armaduras", "Cancelar";

		// -----------------------------------------------------------
		//  Quest items -- do NOT use a reward item more than once!
		//	Add(<shop ID>,<reward ID>,<reward amount>,
		//	    <Zeny cost>,<point cost>,
		//	    <required item ID>,<required item amount>{,...});
		// -----------------------------------------------------------
		
		// Get quests from custom_quests_db 
		
		.nb = query_sql("select * from `custom_quests_db`", .id, .active, .id1, .qtd1, .id2, .qtd2, .id3, .qtd3, .id4, .qtd4, .id5, .qtd5, .id6, .qtd6, .id7, .qtd7, .id8, .qtd8, .id9, .qtd9, .id10, .qtd10, .id11, .qtd11, .id12, .qtd12, .id13, .qtd13, .id14, .qtd14, .id15, .qtd15);

		for(.@i = 0 ; .@i < getarraysize(.id) ; .@i++){
			//announce .active[.@i], 0;
			if(.active[.@i] == 1){
				set .@query$, "select `equip_locations` from `item_db_re` where id=" + .id[.@i];
				.nb = query_sql(.@query$, .@location);
				Add(convertype(.@location), .id[.@i], 1, 0, 0, .id1[.@i], .qtd1[.@i], .id2[.@i], .qtd2[.@i], .id3[.@i], .qtd3[.@i], .id4[.@i], .qtd4[.@i], .id5[.@i], .qtd5[.@i], .id6[.@i], .qtd6[.@i], .id7[.@i], .qtd7[.@i], .id8[.@i], .qtd8[.@i], .id9[.@i], .qtd9[.@i], .id10[.@i], .qtd10[.@i], .id11[.@i], .qtd11[.@i], .id12[.@i], .qtd12[.@i], .id13[.@i], .qtd13[.@i], .id14[.@i], .qtd14[.@i], .id15[.@i], .qtd15[.@i]);
			}
		}
	
		// -----------------------------------------------------------

		freeloop(0);
		set .menu$,"";
		for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
			if(.@i < getarraysize(.Shops$)-1){
				.menu$ = .menu$ + $option_color$+.Shops$[.@i];
			}else{
				.menu$ = .menu$ + $warning_color$+.Shops$[.@i];
			}
			if(.@i < getarraysize(.Shops$)-1){
				.menu$ = .menu$ + ":";
			}
			npcshopdelitem "qshop"+.@i,909;
		}
	end;

	OnMenu:
		mes "^000000[ " + $npc_color$ + "Quests" + " ^000000]";
		cutin "ep16_evil102",2;
		fn_salute_mes(strcharinfo(0), "happy1");
		mes $option_color$ + "Gostaria de fazer alguma quest?";
		next;
		
		
		set .@size, getarraysize(@i);
		if (!.@size) set @shop_index, select(.menu$);
		else if (.@size == 1) set @shop_index, @i[0];
		else {
			for(set .@j,0; .@j<.@size; set .@j,.@j+1)
				set .@menu$, menu$+.Shops$[@i[.@j]]+":";
			set @shop_index, @i[select(.@menu$)-1];
		}
		deletearray @i[0],getarraysize(@i);
		if (.Shops$[@shop_index] == "") {
			message strcharinfo(0),"Ocorreu algum erro.";
			end;
		}
		if(@shop_index < getarraysize(.Shops$)-1){
			dispbottom "Select one item at a time.";
			callshop "qshop"+@shop_index,1;
			npcshopattach "qshop"+@shop_index;
		}else{	
			goto OnCancel;
		}
	end;
	
	OnCancel:
		mes "^000000[ " + $npc_color$ + "Quests" + " ^000000]";
		cutin "", 255;
		fn_bye_mes(strcharinfo(0), "lazy2");
		close;
	end;

	OnBuyItem:
		// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... 
		cutin "ep16_evil102",2;
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "^000000[ " + $npc_color$ + "Quests" + " ^000000]";
	mes "Recompença: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requerimentos:";
	disable_items;
	if (.@q[4]) mes Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
		
	if (.@q[5]) mes Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	
	if (.@q[6]) 
		for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2){
			set .@string$, " ";
			.@string$ = Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1]) + Slot(.@q[.@i]);
			if(Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1]) != $ok_color$){
				.@string$ = .@string$ + ((.ShowID)? $item_color$ + " [ID:"+.@q[.@i]+"] ":"") + $option_color$ + " ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
			}
			mes .@string$;
		}
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select($option_color$ + "Obter ^0055FF"+getitemname(.@q[0])+$warning_color$+":Cancelar")) {
			case 1:{
				if (@qe[0]) { 
					mes "^000000[ " + $npc_color$ + "Quests" + " ^000000]";
					mes "Acho que está faltando algo.";
					next;
					goto OnCancel;
				}
				if (!checkweight(.@q[0],.@q[2])) {
					mes "^000000[ " + $npc_color$ + "Quests" + " ^000000]";
					mes "";
					next;
					goto OnCancel;
				}
				if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
				if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
				if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1]*.@q[1];
				getitem .@q[0],.@q[2];
				if (.Announce) callfunc "fn_announcer", "quest", strcharinfo(0), Slot(.@q[0]);
				specialeffect2 EF_FLOWERLEAF;
				goto OnCancel;
			}break;
			case 2:{
				goto OnCancel;
			}break;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;
	
function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) != "null") {
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
		}
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return $ok_color$;
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function convertype {
	set .@new_type, 0;
	
	switch(getarg(0)){
		case 1:{			// Baixo
			.@new_type = 2;
		}break;
		case 256:{			// Cima
			.@new_type = 1; 
		}break;
		case 512:{			// Meio
			.@new_type = 3;
		}break;
		case 513:{			// Meio e Baixo
			.@new_type = 4;
		}break;
		case 768:{			// Cima e Meio
			.@new_type = 5;
		}break;
		case 769:{			// Cima, Topo e Meio
			.@new_type = 6;
		}break;
		case 136:{			// Acessórios
			.@new_type = 7;
		}break;
		case 4:{			// Capas
			.@new_type = 8;
		}break;				
		case 64:{			// Botas
			.@new_type = 9;
		}break;				
		case 32:{			// Escudos
			.@new_type = 10;
		}break;
		case 16:{			// Armaduras
			.@new_type = 11;
		}break;
	}
	
	return .@new_type;
}

}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
-	shop	qshop7	-1,909:-1
-	shop	qshop8	-1,909:-1
-	shop	qshop9	-1,909:-1
-	shop	qshop10	-1,909:-1
-	shop	qshop11	-1,909:-1